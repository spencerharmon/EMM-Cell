= element InnerMembrane isa QInnerMembrane
\symbol IM
\color #6789ab
\symmetries all
\radius 4

== Data Members
u typedef EventWindow.SiteNum SiteNum;
u typedef EMMTypes.ID ID;
u typedef EMMTypes.StoredValue StoredValue;

u Bool suppressLaying;

u ID id;
u Bool setByIdentifier;

u @Override ID & getID(){
u   return id;
u }

u @Override Bool & getSuppressLaying(){
u   return suppressLaying;
u }

u @Override Bool & getSetByIdentifier(){
u   return setByIdentifier;
u }

u @Override Bool & getBonding(){
u   return bonding;
u }

== Rule lay egg 

given I {
.   InnerMembrane & slf = (InnerMembrane&) ew[0];
.   if ($curatom as InnerMembrane){
.     if (($curatom.bonding) && ($curatom.id != slf.id)){
.       return true;
.     }
.   }
.   return false;
. }
given O isa OuterMembrane
change E isa Egg

check @ {
.   InnerMembrane slf = (InnerMembrane) ew[0];
.   if (slf as InnerMembrane) {
.     if (slf.bonding && !slf.suppressLaying) {
.       EMMConstants ec;
.       Random r;
.       if (r.oneIn(ec.egg_lay_odds)){
.         return true;
.       }else{
.         return false;
.       }
.     }
.   }
. }


   IO_O@ -> ..E..



     I             .
    III           ...
   I...I         .....
  II._.II       ...E...
     O     ->      .
     @             .


== Rule look for OMB

vote o isa OuterMembraneBond
change I isa InnerMembraneParentBond
check @ {
.   if ($o.$nvotes > 0u){
.     return true;
.   }
.   return false;
. }
     o          .
    ooo        ...
   ooooo      .....
  ooo@ooo -> ...I...
   ooooo      .....
    ooo        ...
     o          .
