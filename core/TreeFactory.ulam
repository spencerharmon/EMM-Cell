/**
  TreeFactory is a top-level, concrete class that handles the creation of random
  tree genes, especially as it relates to the Grow method.
  This is a central place for parameters and attributes about genes and their growth.
 */
quark TreeFactory{
  typedef QOperator.OperatorType OperatorType;
  typedef QTree.TreeID TreeID;
  typedef QTree.GeneID GeneID;
  Random r;

  constant Unsigned growOdds = 10u;

  Atom createRandomTerminator(TreeID t, GeneID g, TreeID new){
    //we have to keep track of the terminator types here.
    Unsigned terminatorTypes = 2; //zero-base?
    Unsigned roll = r.create(terminatorTypes);
    which(roll){
      case(0u):
      {
        Value v;
        return v.terminatorCreate(t, g);
      }
      case(1u):
      {
        ComIn c;
        return c.terminatorCreate(t, g);
      }
      case(2u):
      {
        VarRef v;
        return v.terminatorCreate(t, g, new);
      }
    }
  }
  Atom createRandomOperator(TreeID t, GeneID g, GeneID t0, GeneID t1){
    OperatorGene og;
    og.treeID = t;
    og.geneID = g;
    og.tree0 = t0;
    og.tree1 = t1;
    OperatorType max = OperatorType.maxof;
    og.op = (OperatorType) r.create(max);
    return og.atomof;
  }

  //roughly grow as-in grow vs full.
  //full is not yet implemented since 
  //tree depth can't be derrived from GeneIDs.
  Atom grow(TreeID t, GeneID g, GeneID t0, GeneID t1, TreeID new){
    if(r.oneIn(2u)){
      return createRandomTerminator(t, g, new);
    }else{
      return createRandomOperator(t, g, t0, t1);
    }
  }
}