/**
  Base class for all emm-cell components that belong inside the membrane.

  \radius 2
  \author Spencer Harmon
  \license lgpl
 */
quark CellContent : QContent{
  Random r;
  SiteUtils su;
  AtomUtils au;
  EventWindow ew;
  DebugUtils du;
  CellUtils cell;
  
  typedef EventWindow.Symmetry Symmetry;
  typedef EventWindow.SiteNum SiteNum;
  typedef WindowScanner.Category Category;
  virtual Void specialBehavior(TransitionSelf & ts){
    return;
  }
  SiteNum swapWithSite(){
    WindowScanner ws;
    EMMClassSV sv;

    Category cQMembrane = ws.allocateCategory();
    Category cCellContent = ws.allocateCategory();
    Category cESymmetry = ws.allocateCategory();
    Category cEmpty = ws.allocateCategory();

    sv.setQMembrane(cQMembrane);
    sv.setCellContent(cCellContent);
    sv.setESymmetry(cESymmetry);
    sv.setEmpty(cEmpty);

    ws.scan(sv);

    if(ws.getHits(cESymmetry) > 0u){
      Atom a = ew[ws.getSiteNum(cESymmetry)];
      if(a as ESymmetry){
        ew.changeSymmetry(a.symmetry);
	Atom forward = ew[1];
	if(forward as Empty){
	  //Directional movement. We know our symmetry, and the forward direction is empty.
	  return (SiteNum) 0; //disable for breach testing.
//	  return (SiteNum) 1;
	}
      }
    }
    //Directional movement didn't occur. 
    if(ws.getHits(cQMembrane) == 0u){
      //Not on the surface.
      if(ws.getHits(cEmpty) > 0u){
        //Remove Bubbles.
        return ws.getSiteNum(cEmpty);
      }else if(ws.getHits(cCellContent) > 0u){
        //Churning
        return ws.getSiteNum(cCellContent);
      }
    }else{
      //on the surface
      if(ws.getHits(cCellContent) > 0u){
        //Churning
        return ws.getSiteNum(cCellContent);
      }
    }
    return (SiteNum) 0;
  }
  @Override Bool evaluateRuleSets(){
    TransitionSelf ts;
    ts.whoAmI(self.atomof);
    specialBehavior(ts);
    
    SiteNum site = swapWithSite();
    if(site != (SiteNum) 0){
      ts.delaySwap(site);
    }
    ts.commit();
    
    return true;
  }
}