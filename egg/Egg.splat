= element Egg
\symbol Eg
\color #FFFFEE
\symmetries all

== Data Members
u XTimer(4, 8u, 1) age;

== Rule abort
vote s {
.   if ($curatom as EggShell){
.     if ($curatom.abort){
.       return 1u;
.     }
.   }
.   return 0u;
. }
check @ {
.   if ($s.$nvotes > 0u){
.     return true;
.   }
.   return false;
. }

change e isa Empty

 sss    ...
 s@s -> .e.
 sss    ...


== Rule1 (grow shell)

vote e isa Empty

check @ {
.   DebugUtils du;
.   if ( $e.$nvotes > 0u ){
.     EggShell s;
.     ew[$e.$winsn] = s.atomof;
.     return true;
.   }
.   return false;
. }


 eee    ...
 e@e -> ...
 eee    ...

== Rule 2 die if child nearby
vote s : ($curatom is ChildInnerMembrane
.     || $curatom is OuterMembraneBond
.     || $curatom is InnerMembraneParentBond)

     s            .
    sss          ...
   sssss        .....
  ss...ss      .......
 sss.@.sss -> ...._....
  ss...ss      .......
   sssss        .....
    sss          ...
     s            .

== Rule (mature)
given s isa EggShell
vote i isa QInnerMembrane
vote I {
.   if ( $i.$nvotes > 0u ){
.     Atom i = ew[$i.$winsn];
.     if (i as QInnerMembrane){
.       Atom a = ew[$cursn];
.       if (a as QInnerMembrane){
.         if (a.getID() != i.getID()){
.           return (Votes) 1;
.         }
.       }
.     }
.   }
.   return (Votes) 0;
. }

vote p isa QOuterMembrane

vote e isa Empty
change b isa OuterMembraneBond

change y isa Yolk1
change i isa ChildInnerMembrane
change o isa OuterMembrane

  .....      ooooo
  .sss.      oiiio
 ips@spI -> .biyib.
  .sss.      oiiio
  .....      ooooo

  .....     ooooo
  .sss.     oiiio
 ips@s. -> .biyib
  .sss.     oiiio
  ..p..     ooboo
    I         .

    I         .
  ..p..     ooboo
  .sss.     oiiio
 ips@s. -> .biyib
  .sss.     oiiio
  .....     ooooo


== Rule 4 (age)
check @ {
.   DebugUtils du;
.   Egg & slf = (Egg&) ew[0];
.   if (slf.age.countAlarm()){
.     du.print("Egg died in age rule.");
.     return false;
.   }
.   return true;
. }

 @ -> .

change E isa Empty

 @ -> E