= element Egg
\symbol Eg
\color #FFFFEE
\symmetries all

== Data Members
u XTimer(4, 15, 1) age;

== Rule1 (grow shell)

vote e {
.   Atom a = $curatom;
.   if (a as Empty) {
.     return 1u;
.   }else if (a as Egg) {
.     DebugUtils du;
.     du.print("Look. An egg.");
.     return 1u;
.   }
.   return 0u;
. }


check @ {
.   DebugUtils du;
.   if ( $e.$nvotes > 0u ){
.     EggShell s;
.     ew[$e.$winsn] = s.atomof;
.     return true;
.   }
.   return false;
. }

 eeeee    .....
 eeeee    .....
 ee@ee -> .....
 eeeee    .....
 eeeee    .....

== Rule 3 (mature)
given s isa EggShell
vote p isa OuterMembrane
vote P {
.   if ( $p.$nvotes > 0u ){
.     OuterMembrane p = (OuterMembrane) ew[$p.$winsn];
.     Atom me = ew[$cursn];
.     if (me as OuterMembrane) {
.       if (me.id != p.id) {
.         return 1u;
.       }
.     }
.   }
.   return 0u;
. }

vote e isa Empty
change b isa OuterMembraneChildBond
change B isa OuterMembraneParentBond
change y isa Yolk
change i isa InnerMembrane
change o isa OuterMembrane

  sssss      ooooo
  sssss      oiiio
 pss@ssP -> BbiyibB
  sssss      oiiio
  sssss      ooooo

  sssss      ooooo
 psssss     Bbiiio
  ss@ssP ->  oiyibB
  sssss      oiiio
  sssss      ooooo

  sssss      ooooo
  sssss      oiiio
  ss@ssP ->  oiyibB
 psssss     Bbiiio
  sssss      ooooo

  sssss      ooooo
  sssssP     oiiibB
  ss@ss  ->  oiyio
 psssss     Bbiiio
  sssss      ooooo





== Rule 4 (age)
check @ {
.   DebugUtils du;
.   Egg & slf = (Egg&) ew[0];
.   if (slf.age.countAlarm()){
.     du.print("Egg died in age rule.");
.     return false;
.   }
.   return true;
. }

 @ -> .

change E isa Empty

 @ -> E