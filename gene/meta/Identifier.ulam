/**
 Identifies a cell. Not unique.
 Tickles the InnerMembrane watchdog. A membrane without an Identifier is not a cell and therefore dies.
 Randomly changes to EDeath.
 TODO: creates Parent[AB]ID for child cells.
 \color #f33
 \radius 2
 */
element Identifier : QGene {
  typedef EMMTypes.ID ID;
  ID id;

 Void writeIDToMembrane(){
   for(SiteNum i = 1; ew.isAccessible(i); ++i){
     Atom & a = ew[i];
     if(a as QInnerMembrane){
       ID & gid = a.getID();
       gid = id;
       Bool & gsbi = a.getSetByIdentifier();
       gsbi = true;
     }
   }
 }

  Void killCopies(){
    for(SiteNum i = 1; ew.isAccessible(i); ++i){
      Atom a = ew[i];
      if(a as Identifier){
        if(a.id == self.id){
	  Empty e;
	  ew[i] = e.atomof;
	}
      }
    }
  }
 
 @Override Void specialGeneBehavior(TransitionSelf & ts){
    writeIDToMembrane();
    cell.tickleWatchdog();
    if (copying()){
      DebugUtils du;
      du.print("Identifier copying");
      Atom a = copy.parentgenecopy_from_gene(self.atomof);
      cell.putInEmpty(a);
    }
    if(r.oneIn(ec.cellDeathOdds)){
      EDeath ed;
      ts.changeSelf(ed.atomof);
    }
    killCopies();
  }
}
